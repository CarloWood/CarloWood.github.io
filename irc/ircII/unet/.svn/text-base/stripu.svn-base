if (0) {
     Script: unet/stripu
        URL: http://www.xs4all.nl/~carlo17/irc/ircII/unet/stripu
Description: ircII script to strip off "undernet.org" from long server names.
             stripu() returns a string of maximum 16 characters wide.
Also needed: http://www.xs4all.nl/~carlo17/irc/ircII/unet/strlen
    Version: $Id$
  First Ver: 02/05/1998
     Author: ircii @t alinoe.com, Run @t IRC
}

load unet/strlen
if (loaded != [unet.strlen] || version < 101) {
  echo *** unet/stripu  needs http://www.xs4all.nl/~carlo17/irc/ircII/unet/strlen
}

alias stripu {
  if (match(*.undernet.org $0) || match(*.openprojects.net $0)) {
    ^assign f_r $left($rindex(. $left($rindex(. $0) $0)) $0).*;
  } {
    ^assign f_r $0;
  }
  if (strlen($f_r) > 16) {
    @dummy=strlen($f_r)-15;
    if (match(*-r $left($index(. $f_r) $f_r))) {
      assign function_return $left(${index(. $f_r)-dummy+2} $f_r)*-r$mid($index(. $f_r) 64 $f_r);
    } {
      assign function_return $left(${index(. $f_r)-dummy} $f_r)*$mid($index(. $f_r) 64 $f_r);
    }
  } {
    assign function_return $f_r;
  }
}

@version=102
@loaded=[unet.stripu]
